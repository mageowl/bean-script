print(" == Counting Loops")
fn(<counter>) -> {
	else(if(exists(param(0))) -> {
		set(param(0)) -> add(num(query(param(0))), 1)
	}) -> {
		let(param(0)) -> 0
	}
	yield()
}

repeat(3) -> counter(<i>) -> {
	print(add("index: ", str(i)))
} del(<i>)

print("") print(" == Match statements")
fn(<matchVal>) -> {
	print(add("matching ", str(param(0))))
	print(match(param(0)) -> {
		case("abe") -> {
			return("OMG, abe!")
		}
		case("jenny") -> {
			return("OMG, jenny!")
		}
		default -> {
			return("Unkown person")
		}
	})
}

matchVal("abe")
matchVal("joe")
matchVal("jenny")

print("") print(" == For Loops")
obj(<list1>) -> list("a", "b", "c")
print(list1)
list1.for -> {
	print(add("Item is ", item))
}

print("") print(" == Map Loops")
obj(<map1>) -> map("robot", "fire", "wyrm", "water", "golem", "earth")
print(map1)
map1.for -> {
	print(add(key, mul(" ", sub(6, size(key))), "| ", value))
}

